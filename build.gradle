/*
 Copyright (C) 2017 Electronic Arts Inc.  All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:

 1.  Redistributions of source code must retain the above copyright
     notice, this list of conditions and the following disclaimer.
 2.  Redistributions in binary form must reproduce the above copyright
     notice, this list of conditions and the following disclaimer in the
     documentation and/or other materials provided with the distribution.
 3.  Neither the name of Electronic Arts, Inc. ("EA") nor the names of
     its contributors may be used to endorse or promote products derived
     from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY ELECTRONIC ARTS AND ITS CONTRIBUTORS "AS IS" AND ANY
 EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 DISCLAIMED. IN NO EVENT SHALL ELECTRONIC ARTS OR ITS CONTRIBUTORS BE LIABLE FOR ANY
 DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    ext {
        // Orbit
        orbitGroup = "cloud.orbit"
        orbitVersion = "2.0.0-SNAPSHOT"
        orbitWebsite = "http://www.orbit.cloud"
        orgName = "Electronic Arts"
        orgWebsite = "http://www.ea.com"

        // Spring
        springBootVersion = "2.0.0.M6"
        reactorVersion = "3.1.1.RELEASE"

        // Kotlin
        kotlinVersion = "1.1.51"

        // JDK Versions
        jdkSource = "1.8"
        jdkTarget = "1.8"

        // Tests
        junitJupiterVersion  = "5.0.1"
        junitPlatformVersion = "1.0.1"

        // Misc
        slf4jVersion = "1.7.25"
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
    }

    repositories {
        mavenCentral()
    }
}

allprojects {
    group = orbitGroup
    version = orbitVersion
}

// All Subprojects
subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }
}

// Kotlin/Java Projects
configure([
        // Framework
        project(":framework:orbit-core"),

        // Samples
        project(":samples:helloworld"),
]) {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "kotlin-spring"
    apply plugin: "org.junit.platform.gradle.plugin"

    compileJava   {
        sourceCompatibility = jdkSource
        targetCompatibility = jdkTarget
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = jdkTarget
        }
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Version": project.version,
                    "Implementation-Title": "Orbit ($project.name)",
                    "Implementation-Vendor": orgName
            )
        }
    }

    dependencies {
        compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion")
        compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")

        testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
    }
}
